program ::= statement_list
statement_list ::= statement statement_list | epsilon
statement ::= input_stmt | output_stmt | if_stmt | def_stmt | return_stmt | expr_stmt
input_stmt ::= "INPUT" identifier
output_stmt ::= "OUTPUT" expression expression_tail
expression_tail ::= "," expression expression_tail | epsilon
return_stmt ::= "RETURN" expression
if_stmt ::= "IF" condition "THEN" statement_list else_part "END"
else_part ::= "ELSE" statement_list | epsilon
def_stmt ::= "DEF" identifier "(" parameters_opt ")" "BEGIN" statement_list "END"
parameters_opt ::= parameters | epsilon
parameters ::= identifier parameters_tail
parameters_tail ::= "," identifier parameters_tail | epsilon
expr_stmt ::= expression
expression ::= term expression_rest
expression_rest ::= operator term expression_rest | epsilon
term ::= base_term postfix_opt
postfix_opt ::= "T" | epsilon
base_term ::= identifier | number | "[" vector_elements "]" | "MATRIX" "(" matrix_rows ")" | "(" expression ")" | function_call
vector_elements ::= expression vector_tail
vector_tail ::= "," expression | epsilon
matrix_rows ::= "[" vector_elements "]" matrix_row_tail
matrix_row_tail ::= "," "[" vector_elements "]" matrix_row_tail | epsilon
function_call ::= identifier "(" arguments_opt ")"
arguments_opt ::= arguments | epsilon
arguments ::= expression arguments_tail
arguments_tail ::= "," expression arguments_tail | epsilon
condition ::= expression comparison_op expression
operator ::= "=" (* assignment *) | "+" (* vector/matrix addition *) | "-" (* subtraction *) | "*" (* scalar or matrix multiplication *) | "." (* dot product *) | "x" (* cross product *)
comparison_op ::= "==" | "!=" | ">" | "<" | ">=" | "<="
identifier ::= letter identifier_tail
identifier_tail ::= letter_or_digit_or_underscore identifier_tail | epsilon
letter_or_digit_or_underscore ::= letter | digit | "_"
number ::= "0" | ["+" | "-"] nonzero_digit {"0" | nonzero_digit}
nonzero_digit ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" 
digit ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
letter ::= "A" | ... | "Z" | "a" | ... | "z"
